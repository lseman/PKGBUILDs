# Maintainer: Laio O. Seman <laio [at] ieee.org>

_pkgname="hyprlang"
pkgname="${_pkgname}-git"
pkgver=0.2.1.r3.4c28464
pkgrel=1
pkgdesc="The hypr configuration language."
arch=(any)
url="https://github.com/hyprwm/hyprlang"
license=('GPL')
makedepends=('git' 'cmake')
source=("${_pkgname}::git+https://github.com/hyprwm/hyprlang.git")
conflicts=("${_pkgname}")
provides=("lib${_pkgname}.so")
sha256sums=('SKIP')
#options=(!makeflags !buildflags !strip)

pkgver() {
    cd "$_pkgname"
    git describe --long --tags | sed 's/^v//;s/\([^-]*-\)g/r\1/;s/-/./g'
}

build() {
    cd $srcdir/$_pkgname
    cmake -B build \
        -D CMAKE_INSTALL_PREFIX=/usr \
        -D CMAKE_BUILD_TYPE=Release
    cmake --build build
}

package() {
    cd $srcdir/$_pkgname

    DESTDIR="$pkgdir" cmake --install build
    install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE

}
