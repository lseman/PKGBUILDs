# Maintainer: Morgan <morganamilo@archlinux.org>
# PGO Version: Laio Seman <laio@ieee.org>

pkgname=paru
pkgver=2.0.1
pkgrel=1
pkgdesc='Feature packed AUR helper'
url='https://github.com/morganamilo/paru'
source=($pkgname-$pkgver.tar.gz::https://github.com/Morganamilo/paru/archive/v$pkgver.tar.gz
    optimize.patch)
backup=("etc/paru.conf")
arch=('i686' 'pentium4' 'x86_64' 'arm' 'armv7h' 'armv6h' 'aarch64')
license=('GPL3')
makedepends=('cargo')
depends=('git' 'pacman')
optdepends=('bat: colored pkgbuild printing' 'devtools: build in chroot and downloading pkgbuilds')
sha256sums=('47cae94d227cc08f86706408d234444af6dda192ba24309c251a0b43a8aa7980'
    'f1b37082982412bf5f4449e28e8500665108c8969581c29c1b456145237684d7')

prepare() {
    cd "$pkgname-$pkgver"
    cargo fetch --locked --target "$(rustc -vV | sed -n 's|host: ||p')"
    patch -p1 -i ../optimize.patch
}

build() {

    export MALLOC_CONF="thp:always,metadata_thp:always"

    cd "$srcdir/$pkgname-$pkgver"

    if pacman -T pacman-git >/dev/null; then
        _features+="git,"
    fi

    cargo pgo build -- --frozen --features "${_features:-}" --release --target-dir target
    cargo pgo test
    cargo pgo run
    cargo pgo optimize build -- --frozen --features "${_features:-}" --release --target-dir target
    ./scripts/mkmo locale/
}

package() {
    cd "$srcdir/$pkgname-$pkgver"

    install -Dm755 target/release/paru "${pkgdir}/usr/bin/paru"
    install -Dm644 paru.conf "${pkgdir}/etc/paru.conf"

    install -Dm644 man/paru.8 "$pkgdir/usr/share/man/man8/paru.8"
    install -Dm644 man/paru.conf.5 "$pkgdir/usr/share/man/man5/paru.conf.5"

    install -Dm644 completions/bash "${pkgdir}/usr/share/bash-completion/completions/paru.bash"
    install -Dm644 completions/fish "${pkgdir}/usr/share/fish/vendor_completions.d/paru.fish"
    install -Dm644 completions/zsh "${pkgdir}/usr/share/zsh/site-functions/_paru"

    install -d "$pkgdir/usr/share/"
    cp -r locale "$pkgdir/usr/share/"
}
