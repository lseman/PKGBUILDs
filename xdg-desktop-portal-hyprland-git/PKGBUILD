# Maintainer: ThatOneCalculator <kainoa@t1c.dev>
# Based on the xdg-desktop-portal-wlr-git PKGBUILD

_pkgname="xdg-desktop-portal-hyprland"
pkgname="${_pkgname}-git"
pkgver=1.3.1.r5.6a5de92
_protocolver=4d29e48433270a2af06b8bc711ca1fe5109746cd
pkgrel=1
epoch=1
pkgdesc="xdg-desktop-portal backend for hyprland"
url="https://github.com/hyprwm/xdg-desktop-portal-hyprland"
arch=(x86_64)
license=(custom:MIT)
provides=("${pkgname%-git}" "xdg-desktop-portal-impl" "xdg-desktop-portal-wlr")
conflicts=("${pkgname%-git}" "xdg-desktop-portal-wlr")
depends=(gcc-libs
    glibc
    libinih
    pipewire
    qt6-base
    qt6-wayland
    sdbus-cpp
    util-linux-libs libuuid.so
    wlroots
    xdg-desktop-portal)
makedepends=(cmake
    wayland
    wayland-protocols)

optdepends=(
    "grim: required for the screenshot portal to function"
    "slurp: support for interactive mode for the screenshot portal; one of the built-in chooser options for the screencast portal"
    "bemenu: one of the built-in chooser options for the screencast portal"
    "wofi: one of the built-in chooser options for the screencast portal"
    "hyprland: the Hyprland compositor"
)
source=("${_pkgname}::git+https://github.com/hyprwm/xdg-desktop-portal-hyprland.git"
    "https://github.com/hyprwm/hyprland-protocols/archive/$_protocolver.tar.gz"
    hyprland-portals.conf) # TODO move this to hyprland where it belongs)
sha256sums=('SKIP'
    '5f6be4d870e94314f05ec7ff9c9c1f028748230ff80a7f89aeaf66c08e5c70e0'
    '20bc215211f16a361086d59fa051df7337d95f91c695a29d8c5d23d40407fad5')

pkgver() {
    cd "$_pkgname"
    git describe --long --tags | sed 's/^v//;s/\([^-]*-\)g/r\1/;s/-/./g'
}

build() {
    cd "${srcdir}/${_pkgname}"
    cd subprojects
    rm -rf hyprland-protocols sdbus-cpp
    ln -sfT "$srcdir/hyprland-protocols-$_protocolver" hyprland-protocols
    cd ..

    cmake -B build \
        -D CMAKE_INSTALL_PREFIX=/usr \
        -D CMAKE_INSTALL_LIBEXECDIR=/usr/lib \
        -D CMAKE_BUILD_TYPE=Release
    cmake --build build

}

package() {
    depends=(xdg-desktop-portal)
    cd "${srcdir}/${_pkgname}"
    DESTDIR="$pkgdir" cmake --install build
    install -Dm0644 -t "$pkgdir/usr/share/xdg-desktop-portal/" "${srcdir}/hyprland-portals.conf"
}
