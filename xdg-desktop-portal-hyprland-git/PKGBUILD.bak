# Maintainer: ThatOneCalculator <kainoa@t1c.dev>
# Based on the xdg-desktop-portal-wlr-git PKGBUILD

_pkgname="xdg-desktop-portal-hyprland"
pkgname="${_pkgname}-git"
pkgver=1.3.0.r7.1c80212
pkgrel=1
epoch=1
pkgdesc="xdg-desktop-portal backend for hyprland"
url="https://github.com/hyprwm/xdg-desktop-portal-hyprland"
arch=(x86_64)
license=(custom:MIT)
provides=("${pkgname%-git}" "xdg-desktop-portal-impl" "xdg-desktop-portal-wlr")
conflicts=("${pkgname%-git}" "xdg-desktop-portal-wlr")
depends=(pipewire libinih qt6-base qt6-wayland sdbus-cpp)
makedepends=(git meson wayland-protocols wayland scdoc cmake)
optdepends=(
    "grim: required for the screenshot portal to function"
    "slurp: support for interactive mode for the screenshot portal; one of the built-in chooser options for the screencast portal"
    "bemenu: one of the built-in chooser options for the screencast portal"
    "wofi: one of the built-in chooser options for the screencast portal"
    "hyprland: the Hyprland compositor"
)
source=("${_pkgname}::git+https://github.com/hyprwm/xdg-desktop-portal-hyprland.git")
sha256sums=('SKIP')

pkgver() {
    cd "$_pkgname"
    git describe --long --tags | sed 's/^v//;s/\([^-]*-\)g/r\1/;s/-/./g'
}

build() {
    cd "${srcdir}/${_pkgname}"
    export LDFLAGS="$LDFLAGS -lhyprlang"
    export CFLAGS="$CFLAGS -I/usr/include/libdrm/"
    export CXXFLAGS="$CXXFLAGS -I/usr/include/libdrm/"
    git submodule update --init
    meson build --prefix=/usr
    ninja -C build
    cd hyprland-share-picker && make all && cd ..
}

package() {
    depends=(xdg-desktop-portal)
    cd "${srcdir}/${_pkgname}"
    DESTDIR="${pkgdir}" ninja -C build install
    install -Dm755 hyprland-share-picker/build/hyprland-share-picker -t "${pkgdir}/usr/bin"
    install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${_pkgname}"
}
